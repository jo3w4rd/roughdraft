<!DOCTYPE html>
<!--[if IE]><![endif]-->
<html>
  
  <head>
    <script>(function(w,d,s,l,i){w[l]=w[l]||[];w[l].push({'gtm.start':  new Date().getTime(),event:'gtm.js'});var f=d.getElementsByTagName(s)[0],   j=d.createElement(s),dl=l!='dataLayer'?'&l='+l:'';j.async=true;j.src=   'https://www.googletagmanager.com/gtm.js?id='+i+dl;f.parentNode.insertBefore(j,f);  })(window,document,'script','dataLayer','GTM-MC35ML');</script>
    <meta charset="utf-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge,chrome=1">
  
    <meta http-equiv="Cache-Control" content="no-cache, no-store, must-revalidate">
    <meta http-equiv="Pragma" content="no-cache">
    <meta http-equiv="Expires" content="0">
  
    <title>Vertex and fragment shader examples | Unity Technologies Developer Guide </title>
    <meta name="viewport" content="width=device-width">
    <meta name="title" content="Vertex and fragment shader examples | Unity Technologies Developer Guide ">
    <meta name="generator" content="docfx 2.40.7.0">
    
    <link rel="shortcut icon" href="../../favicon.ico">
    <link rel="stylesheet" href="../../styles/docfx.vendor.css">
    <link rel="stylesheet" href="../../styles/docfx.css">
    <link rel="stylesheet" href="../../styles/main.css">
    <meta property="docfx:navrel" content="../../toc.html">
    <meta property="docfx:tocrel" content="toc.html">
    <meta property="docfx:rel" content="../../">
    
  </head>
  <body data-spy="scroll" data-target="#affix" class="manual">
	<noscript><iframe src="https://www.googletagmanager.com/ns.html?id=GTM-MC35ML" height="0" width="0" style="display:none;visibility:hidden"></iframe></noscript>
    <div id="wrapper">
      <header>
        
        <nav id="autocollapse" class="navbar navbar-inverse ng-scope" role="navigation">
          <div class="container">
            <div class="navbar-header">
              <button type="button" class="navbar-toggle" data-toggle="collapse" data-target="#navbar">
                <span class="sr-only">Toggle navigation</span>
                <span class="icon-bar"></span>
                <span class="icon-bar"></span>
                <span class="icon-bar"></span>
              </button>
              
              <a class="navbar-brand" href="../../index.html">
                <img id="logo" class="svg" src="../../logo.svg" alt="">
              </a>
            </div>
            <div class="collapse navbar-collapse" id="navbar">
              <form class="navbar-form navbar-right" role="search" id="search">
                <div class="form-group">
                  <input type="text" class="form-control" id="search-query" placeholder="Search" autocomplete="off">
                </div>
        
        		<div class="back-to-unity-group">
        			<a class="back-to-unity" href="http://docs.unity3d.com/">docs.unity3d.com</a>
        		</div>
        
              </form>
            </div>
          </div>
        </nav>
        
        <div class="subnav navbar navbar-default">
          <div class="container hide-when-search" id="breadcrumb">
            <ul class="breadcrumb">
              <li></li>
            </ul>
          </div>
        </div>
      </header>
      <div class="container body-content">
        
        <div id="search-results">
          <div class="search-list"></div>
          <div class="sr-items"></div>
          <ul id="pagination"></ul>
        </div>
      </div>
      <div role="main" class="container body-content hide-when-search">
        
        <div class="sidenav hide-when-search">
          <a class="btn toc-toggle collapse" data-toggle="collapse" href="#sidetoggle" aria-expanded="false" aria-controls="sidetoggle">Show / Hide Table of Contents</a>
          <div class="sidetoggle collapse" id="sidetoggle">
            <div id="sidetoc"></div>
          </div>
        </div>
        <div class="article row grid-right">
          <div class="col-md-10">
            <article class="content wrap" id="_content" data-uid="">
<h1 id="vertex-and-fragment-shader-examples">Vertex and fragment shader examples</h1>

<p>This page contains vertex and fragment program examples.
For a basic introduction to shaders, see the shader tutorials:
<a href="ShaderTut1.html">Part 1</a> and <a href="ShaderTut2.html">Part 2</a>. For an easy way of writing regular material shaders, see <a href="SL-SurfaceShaders.html">Surface Shaders</a>.</p>
<p>You can download the examples shown below as a <a href="../uploads/Examples/UnityShaderDocExamples.zip">zipped Unity project</a>.</p>
<h3 id="setting-up-the-scene">Setting up the scene</h3>
<p>If you are not familiar with Unity&#39;s <strong>Scene View</strong>, <strong>Hierarchy View</strong>,
<strong>Project View</strong> and <strong>Inspector</strong>, now would be a good time to read the
first few sections from the manual, starting with <a href="UnityBasics.html">Unity Basics</a>.</p>
<p>The first step is to create some objects which you will use to test your shaders. Select <strong>Game Object</strong> &gt; <strong>3D Object</strong> &gt; <strong>Capsule</strong> in the main menu. Then position the camera so it shows the capsule. Double-click the Capsule in the Hierarchy to
focus the scene view on it, then select the Main Camera object and click <strong>Game object</strong> &gt; <strong>Align with View</strong>
from the main menu.</p>
<p><img src="../uploads/Main/SL-CapsuleSetup.png" alt=""></p>
<p>Create a new <a href="Materials.html">Material</a> by selecting <strong>Create</strong> &gt; <strong>Material</strong> from the menu in the Project View. A new material called <em>New Material</em> will appear in the Project View.</p>
<p><img src="../uploads/Main/SL-NewMaterial.png" alt=""></p>
<h3 id="creating-a-shader">Creating a shader</h3>
<p>Now create a new <a href="Shaders.html">Shader</a> asset in a similar way. Select <strong>Create</strong> &gt; <strong>Shader</strong> &gt; <strong>Unlit Shader</strong> from the menu in the Project View.
This creates a basic shader that just displays a texture without any lighting.</p>
<p><img src="../uploads/Main/SL-NewShader.png" alt=""></p>
<p>Other entries in the <strong>Create</strong> &gt; <strong>Shader</strong> menu create barebone shaders
or other types, for example a basic <a href="SL-SurfaceShaders.html">surface shader</a>.</p>
<h3 id="linking-the-mesh-material-and-shader">Linking the mesh, material and shader</h3>
<p>Make the material use the shader via the material&#39;s inspector, or just drag the shader asset over the material asset in the Project View. The material inspector will display a white sphere when it uses this shader.</p>
<p><img src="../uploads/Main/SL-SelectShaderMenu.png" alt=""></p>
<p>Now drag the material onto your mesh object in either the Scene or the Hierarchy views. Alternatively, select the object, and in the inspector make it use the material in the <a href="class-MeshRenderer.html">Mesh Renderer</a> component&#39;s Materials slot.</p>
<p><img src="../uploads/Main/SL-WhiteCapsuleUnlitShader.png" alt=""></p>
<p>With these things set up, you can now begin looking at the shader code, and you will see the results of your changes to the shader on the capsule in the Scene View.</p>
<h2 id="main-parts-of-the-shader">Main parts of the shader</h2>
<p>To begin examining the code of the shader, double-click the shader asset in the Project View. The shader code will open in your script editor (MonoDevelop or Visual Studio).</p>
<p>The shader starts off with this code:</p>
<pre><code>Shader &quot;Unlit/NewUnlitShader&quot;
{
    Properties
    {
        _MainTex (&quot;Texture&quot;, 2D) = &quot;white&quot; {}
    }
    SubShader
    {
        Tags { &quot;RenderType&quot;=&quot;Opaque&quot; }
        LOD 100

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // make fog work
            #pragma multi_compile_fog

            #include &quot;UnityCG.cginc&quot;

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float2 uv : TEXCOORD0;
                UNITY_FOG_COORDS(1)
                float4 vertex : SV_POSITION;
            };

            sampler2D _MainTex;
            float4 _MainTex_ST;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                UNITY_TRANSFER_FOG(o,o.vertex);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // sample the texture
                fixed4 col = tex2D(_MainTex, i.uv);
                // apply fog
                UNITY_APPLY_FOG(i.fogCoord, col);
                return col;
            }
            ENDCG
        }
    }
}
</code></pre><p>This initial shader does not look very simple! But don&#39;t worry,
we will go over each part step-by-step.</p>
<p>Let&#39;s see the main parts of our simple shader.</p>
<pre><code>Shader
</code></pre><p>The <a href="SL-Shader.html">Shader</a> command contains a string with the name of
the shader. You can use forward slash characters &quot;/&quot; to place your shader in sub-menus when selecting your shader in the <a href="class-Material.html">Material</a> inspector.</p>
<pre><code>Properties
</code></pre><p>The <a href="SL-Properties.html">Properties</a> block contains shader variables
(textures, colors etc.) that will be saved as part of the Material,
and displayed in the material inspector. In our unlit shader template,
there is a single texture property declared.</p>
<pre><code>SubShader
</code></pre><p>A Shader can contain one or more <a href="SL-SubShader.html">SubShaders</a>, which are
primarily used to implement shaders for different GPU capabilities.
In this tutorial we&#39;re not much concerned with that, so all our
shaders will contain just one SubShader.</p>
<pre><code>Pass
</code></pre><p>Each SubShader is composed of a number of <a href="SL-Pass.html">passes</a>, and
each Pass represents an execution of the vertex and fragment code
for the same object rendered with the material of the shader.
Many simple shaders use just one pass, but shaders that
interact with lighting might need more (see
<a href="SL-RenderPipeline.html">Lighting Pipeline</a> for details). Commands
inside Pass typically setup fixed function state, for example
blending modes.</p>
<pre><code>CGPROGRAM .. ENDCG
</code></pre><p>These keywords surround portions of HLSL code within the vertex and fragment
shaders. Typically this is where most of the interesting code is. See
<a href="SL-ShaderPrograms.html">vertex and fragment shaders</a> for details.</p>
<h2 id="simple-unlit-shader">Simple unlit shader</h2>
<p>The unlit shader template does a few more things than would be
absolutely needed to display an object with a texture. For example,
it supports Fog, and texture tiling/offset fields in the material. 
Let&#39;s simplify the shader to bare minimum, and add more comments:</p>
<pre><code>Shader &quot;Unlit/SimpleUnlitTexturedShader&quot;
{
    Properties
    {
        // we have removed support for texture tiling/offset,
        // so make them not be displayed in material inspector
        [NoScaleOffset] _MainTex (&quot;Texture&quot;, 2D) = &quot;white&quot; {}
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            // use &quot;vert&quot; function as the vertex shader
            #pragma vertex vert
            // use &quot;frag&quot; function as the pixel (fragment) shader
            #pragma fragment frag

            // vertex shader inputs
            struct appdata
            {
                float4 vertex : POSITION; // vertex position
                float2 uv : TEXCOORD0; // texture coordinate
            };

            // vertex shader outputs (&quot;vertex to fragment&quot;)
            struct v2f
            {
                float2 uv : TEXCOORD0; // texture coordinate
                float4 vertex : SV_POSITION; // clip space position
            };

            // vertex shader
            v2f vert (appdata v)
            {
                v2f o;
                // transform position to clip space
                // (multiply with model*view*projection matrix)
                o.vertex = mul(UNITY_MATRIX_MVP, v.vertex);
                // just pass the texture coordinate
                o.uv = v.uv;
                return o;
            }

            // texture we will sample
            sampler2D _MainTex;

            // pixel shader; returns low precision (&quot;fixed4&quot; type)
            // color (&quot;SV_Target&quot; semantic)
            fixed4 frag (v2f i) : SV_Target
            {
                // sample texture and return it
                fixed4 col = tex2D(_MainTex, i.uv);
                return col;
            }
            ENDCG
        }
    }
}
</code></pre><p>The <strong>Vertex Shader</strong> is a program that runs on each vertex of the 3D model. Quite often it does not do anything particularly interesting. Here we just transform vertex position from object space into so called &quot;clip space&quot;, which is what&#39;s used by the GPU to rasterize the object on screen. We also pass the input texture coordinate unmodified - we&#39;ll need it to sample the texture in the fragment shader.</p>
<p>The <strong>Fragment Shader</strong> is a program that runs on each and every pixel that object occupies on-screen, and is usually used to calculate and output the color of each pixel. Usually there are millions of pixels on the screen, and the fragment shaders are executed
for all of them! Optimizing fragment shaders is quite an important part of overall game performance work.</p>
<p>Some variable or function definitions are followed by a <strong>Semantic Signifier</strong> - for example <strong>: POSITION</strong> or <strong>: SV_Target</strong>. These semantics signifiers communicate the &quot;meaning&quot; of these variables to the GPU. See the <a href="SL-ShaderSemantics.html">shader semantics</a> page for details.</p>
<p>When used on a nice model with a nice texture, our simple shader looks pretty good!</p>
<p><img src="../uploads/SL/ExampleUnlitTextured.png" alt=""> </p>
<h2 id="even-simpler-single-color-shader">Even simpler single color shader</h2>
<p>Let&#39;s simplify the shader even more -- we&#39;ll make a shader that draws the whole object in a single
color. This is not terribly useful, but hey we&#39;re learning here.</p>
<pre><code>Shader &quot;Unlit/SingleColor&quot;
{
    Properties
    {
        // Color property for material inspector, default to white
        _Color (&quot;Main Color&quot;, Color) = (1,1,1,1)
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            // vertex shader
            // this time instead of using &quot;appdata&quot; struct, just spell inputs manually,
            // and instead of returning v2f struct, also just return a single output
            // float4 clip position
            float4 vert (float4 vertex : POSITION) : SV_POSITION
            {
                return mul(UNITY_MATRIX_MVP, vertex);
            }

            // color from the material
            fixed4 _Color;

            // pixel shader, no inputs needed
            fixed4 frag () : SV_Target
            {
                return _Color; // just return it
            }
            ENDCG
        }
    }
}
</code></pre><p>This time instead of using structs for input (<strong>appdata</strong>) and output (<strong>v2f</strong>), the shader functions just spell out inputs manually. Both ways work, and which you choose to use depends on your coding style and preference.</p>
<p><img src="../uploads/SL/ExampleSingleColor.png" alt=""> </p>
<h2 id="using-mesh-normals-for-fun-and-profit">Using mesh normals for fun and profit</h2>
<p>Let&#39;s proceed with a shader that displays mesh normals in world space. Without further ado:</p>
<pre><code>Shader &quot;Unlit/WorldSpaceNormals&quot;
{
    // no Properties block this time!
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            // include file that contains UnityObjectToWorldNormal helper function
            #include &quot;UnityCG.cginc&quot;

            struct v2f {
                // we&#39;ll output world space normal as one of regular (&quot;texcoord&quot;) interpolators
                half3 worldNormal : TEXCOORD0;
                float4 pos : SV_POSITION;
            };

            // vertex shader: takes object space normal as input too
            v2f vert (float4 vertex : POSITION, float3 normal : NORMAL)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(vertex);
                // UnityCG.cginc file contains function to transform
                // normal from object to world space, use that
                o.worldNormal = UnityObjectToWorldNormal(normal);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 c = 0;
                // normal is a 3D vector with xyz components; in -1..1
                // range. To display it as color, bring the range into 0..1
                // and put into red, green, blue components
                c.rgb = i.worldNormal*0.5+0.5;
                return c;
            }
            ENDCG
        }
    }
}
</code></pre><p><img src="../uploads/SL/ExampleWorldSpaceNormals.png" alt=""></p>
<p>Besides resulting in pretty colors, normals are used for all sorts of graphics effects -- lighting, reflections, silhouettes and so on.</p>
<p>In the shader above, we started using one of Unity&#39;s built-in <a href="SL-BuiltinIncludes.html">shader include files</a>.
Here, <strong>UnityCG.cginc</strong> was used which contains a handy function <strong>UnityObjectToWorldNormal</strong>. We have also used the utility function <strong>UnityObjectToClipPos</strong>, which transforms the vertex from object space to the screen. This just makes the code easier to read and is more efficient under certain circumstances.</p>
<p>We&#39;ve seen that data can be passed from the vertex into fragment shader in so-called &quot;interpolators&quot; (or sometimes called &quot;varyings&quot;). In HLSL shading language they are typically labeled with <strong>TEXCOORDn</strong> semantic, and each of them can be up to a 4-component vector (see <a href="SL-ShaderSemantics.html">semantics</a> page for details).</p>
<p>Also we&#39;ve learned a simple technique in how to visualize normalized vectors (in -1.0 to +1.0 range) as colors: just multiply them by half and add half. See more vertex data visualization examples in <a href="SL-VertexProgramInputs.html">vertex program inputs</a> page.</p>
<h4 id="environment-reflection-using-world-space-normals">Environment reflection using world-space normals</h4>
<p>When a <a href="class-Skybox.html">Skybox</a> is used in the scene as a reflection source (see <a href="GlobalIllumination.html">Lighting Window</a>),
then essentially a &quot;default&quot; <a href="class-ReflectionProbe.html">Reflection Probe</a> is created, containing the skybox data.
A reflection probe is internally a <a href="class-Cubemap.html">Cubemap</a> texture; we will extend the world-space normals shader above to look into it.</p>
<p>The code is starting to get a bit involved by now. Of course, if you want shaders that automatically work with lights, shadows, reflections and the rest of the lighting system, it&#39;s way easier to use <a href="SL-SurfaceShaders.html">surface shaders</a>. This example is intended to show you how to use parts of the lighting system in a &quot;manual&quot; way.</p>
<pre><code>Shader &quot;Unlit/SkyReflection&quot;
{
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;

            struct v2f {
                half3 worldRefl : TEXCOORD0;
                float4 pos : SV_POSITION;
            };

            v2f vert (float4 vertex : POSITION, float3 normal : NORMAL)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(vertex);
                // compute world space position of the vertex
                float3 worldPos = mul(_Object2World, vertex).xyz;
                // compute world space view direction
                float3 worldViewDir = normalize(UnityWorldSpaceViewDir(worldPos));
                // world space normal
                float3 worldNormal = UnityObjectToWorldNormal(normal);
                // world space reflection vector
                o.worldRefl = reflect(-worldViewDir, worldNormal);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // sample the default reflection cubemap, using the reflection vector
                half4 skyData = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, i.worldRefl);
                // decode cubemap data into actual color
                half3 skyColor = DecodeHDR (skyData, unity_SpecCube0_HDR);
                // output it!
                fixed4 c = 0;
                c.rgb = skyColor;
                return c;
            }
            ENDCG
        }
    }
}
</code></pre><p><img src="../uploads/SL/ExampleSkyReflection.png" alt=""></p>
<p>The example above uses several things from the built-in <a href="SL-BuiltinIncludes.html">shader include files</a>:</p>
<ul>
<li><strong>unity_SpecCube0</strong>, <strong>unity_SpecCube0_HDR</strong>, <strong>Object2World</strong>, <strong>UNITY_MATRIX_MVP</strong> from the
<a href="SL-UnityShaderVariables.html">built-in shader variables</a>. <strong>unity_SpecCube0</strong> contains data for the active reflection probe.</li>
<li><strong>UNITY_SAMPLE_TEXCUBE</strong> is a <a href="SL-BuiltinMacros.html">built-in macro</a> to sample a cubemap. Most regular cubemaps are declared and
used using standard HLSL syntax (<strong>samplerCUBE</strong> and <strong>texCUBE</strong>), however the reflection probe cubemaps in Unity are declared in a special way to save on sampler slots. If you don&#39;t know what that is, don&#39;t worry, just know that in order to use <strong>unity_SpecCube0</strong> cubemap you have to use <strong>UNITY_SAMPLE_TEXCUBE</strong> macro.</li>
<li><strong>UnityWorldSpaceViewDir</strong> function from <strong>UnityCG.cginc</strong>, and <strong>DecodeHDR</strong> function from the same file. The latter is used to get actual color from the reflection probe data -- since Unity stores reflection probe cubemap in specially encoded way.</li>
<li><strong>reflect</strong> is just a built-in HLSL function to compute vector reflection around a given normal.</li>
</ul>
<h4 id="environment-reflection-with-a-normal-map">Environment reflection with a normal map</h4>
<p>Often <strong>Normal Maps</strong> are used to create additional detail on objects, without creating additional geometry. Let&#39;s see how to make a shader that reflects the environment, with a normal map texture.</p>
<p>Now the math is starting to get <em>really involved</em>, so we&#39;ll do it in a few steps. In the shader above, the reflection
direction was computed per-vertex (in the vertex shader), and the fragment shader was only doing the reflection
probe cubemap lookup. However once we start using normal maps, the surface normal itself needs to be calculated on a per-pixel basis, which means we also have to compute how the environment is reflected per-pixel!</p>
<p>So first of all, let&#39;s rewrite the shader above to do the same thing, except we will move some of the calculations to the fragment shader, so they are computed per-pixel:</p>
<pre><code>Shader &quot;Unlit/SkyReflection Per Pixel&quot;
{
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;

            struct v2f {
                float3 worldPos : TEXCOORD0;
                half3 worldNormal : TEXCOORD1;
                float4 pos : SV_POSITION;
            };

            v2f vert (float4 vertex : POSITION, float3 normal : NORMAL)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(vertex);
                o.worldPos = mul(_Object2World, vertex).xyz;
                o.worldNormal = UnityObjectToWorldNormal(normal);
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                // compute view direction and reflection vector
                // per-pixel here
                half3 worldViewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
                half3 worldRefl = reflect(-worldViewDir, i.worldNormal);

                // same as in previous shader
                half4 skyData = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, worldRefl);
                half3 skyColor = DecodeHDR (skyData, unity_SpecCube0_HDR);
                fixed4 c = 0;
                c.rgb = skyColor;
                return c;
            }
            ENDCG
        }
    }
}
</code></pre><p>That by itself does not give us much -- the shader looks exactly the same, except now it runs slower since it does more calculations for each and every pixel on screen, instead of only for each of the model&#39;s vertices. However, we&#39;ll need these calculations really soon. Higher graphics fidelity often requires more complex shaders.</p>
<p>We&#39;ll have to learn a new thing now too; the so-called &quot;tangent space&quot;. Normal map textures are most often expressed in a coordinate space that can be thought of as &quot;following the surface&quot; of the model. In our shader, we will need to to know the tangent space basis vectors, read the normal vector from the texture, transform it into world space, and then do all the math
from the above shader. Let&#39;s get to it!</p>
<pre><code>Shader &quot;Unlit/SkyReflection Per Pixel&quot;
{
    Properties {
        // normal map texture on the material,
        // default to dummy &quot;flat surface&quot; normalmap
        _BumpMap(&quot;Normal Map&quot;, 2D) = &quot;bump&quot; {}
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;

            struct v2f {
                float3 worldPos : TEXCOORD0;
                // these three vectors will hold a 3x3 rotation matrix
                // that transforms from tangent to world space
                half3 tspace0 : TEXCOORD1; // tangent.x, bitangent.x, normal.x
                half3 tspace1 : TEXCOORD2; // tangent.y, bitangent.y, normal.y
                half3 tspace2 : TEXCOORD3; // tangent.z, bitangent.z, normal.z
                // texture coordinate for the normal map
                float2 uv : TEXCOORD4;
                float4 pos : SV_POSITION;
            };

            // vertex shader now also needs a per-vertex tangent vector.
            // in Unity tangents are 4D vectors, with the .w component used to
            // indicate direction of the bitangent vector.
            // we also need the texture coordinate.
            v2f vert (float4 vertex : POSITION, float3 normal : NORMAL, float4 tangent : TANGENT, float2 uv : TEXCOORD0)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(vertex);
                o.worldPos = mul(_Object2World, vertex).xyz;
                half3 wNormal = UnityObjectToWorldNormal(normal);
                half3 wTangent = UnityObjectToWorldDir(tangent.xyz);
                // compute bitangent from cross product of normal and tangent
                half tangentSign = tangent.w * unity_WorldTransformParams.w;
                half3 wBitangent = cross(wNormal, wTangent) * tangentSign;
                // output the tangent space matrix
                o.tspace0 = half3(wTangent.x, wBitangent.x, wNormal.x);
                o.tspace1 = half3(wTangent.y, wBitangent.y, wNormal.y);
                o.tspace2 = half3(wTangent.z, wBitangent.z, wNormal.z);
                o.uv = uv;
                return o;
            }

            // normal map texture from shader properties
            sampler2D _BumpMap;

            fixed4 frag (v2f i) : SV_Target
            {
                // sample the normal map, and decode from the Unity encoding
                half3 tnormal = UnpackNormal(tex2D(_BumpMap, i.uv));
                // transform normal from tangent to world space
                half3 worldNormal;
                worldNormal.x = dot(i.tspace0, tnormal);
                worldNormal.y = dot(i.tspace1, tnormal);
                worldNormal.z = dot(i.tspace2, tnormal);

                // rest the same as in previous shader
                half3 worldViewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
                half3 worldRefl = reflect(-worldViewDir, worldNormal);
                half4 skyData = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, worldRefl);
                half3 skyColor = DecodeHDR (skyData, unity_SpecCube0_HDR);
                fixed4 c = 0;
                c.rgb = skyColor;
                return c;
            }
            ENDCG
        }
    }
}
</code></pre><p>Phew, that was quite involved. But look, normal mapped reflections!</p>
<p><img src="../uploads/SL/ExampleSkyReflectionNormalmap.png" alt=""></p>
<h2 id="adding-more-textures">Adding more textures</h2>
<p>Let&#39;s add more textures to the normal-mapped, sky-reflecting shader above. We&#39;ll add the base color texture, seen in the first unlit example, and an occlusion map to darken the cavities.</p>
<pre><code>Shader &quot;Unlit/More Textures&quot;
{
    Properties {
        // three textures we&#39;ll use in the material
        _MainTex(&quot;Base texture&quot;, 2D) = &quot;white&quot; {}
        _OcclusionMap(&quot;Occlusion&quot;, 2D) = &quot;white&quot; {}
        _BumpMap(&quot;Normal Map&quot;, 2D) = &quot;bump&quot; {}
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;

            // exactly the same as in previous shader
            struct v2f {
                float3 worldPos : TEXCOORD0;
                half3 tspace0 : TEXCOORD1;
                half3 tspace1 : TEXCOORD2;
                half3 tspace2 : TEXCOORD3;
                float2 uv : TEXCOORD4;
                float4 pos : SV_POSITION;
            };
            v2f vert (float4 vertex : POSITION, float3 normal : NORMAL, float4 tangent : TANGENT, float2 uv : TEXCOORD0)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(vertex);
                o.worldPos = mul(_Object2World, vertex).xyz;
                half3 wNormal = UnityObjectToWorldNormal(normal);
                half3 wTangent = UnityObjectToWorldDir(tangent.xyz);
                half tangentSign = tangent.w * unity_WorldTransformParams.w;
                half3 wBitangent = cross(wNormal, wTangent) * tangentSign;
                o.tspace0 = half3(wTangent.x, wBitangent.x, wNormal.x);
                o.tspace1 = half3(wTangent.y, wBitangent.y, wNormal.y);
                o.tspace2 = half3(wTangent.z, wBitangent.z, wNormal.z);
                o.uv = uv;
                return o;
            }

            // textures from shader properties
            sampler2D _MainTex;
            sampler2D _OcclusionMap;
            sampler2D _BumpMap;

            fixed4 frag (v2f i) : SV_Target
            {
                // same as from previous shader...
                half3 tnormal = UnpackNormal(tex2D(_BumpMap, i.uv));
                half3 worldNormal;
                worldNormal.x = dot(i.tspace0, tnormal);
                worldNormal.y = dot(i.tspace1, tnormal);
                worldNormal.z = dot(i.tspace2, tnormal);
                half3 worldViewDir = normalize(UnityWorldSpaceViewDir(i.worldPos));
                half3 worldRefl = reflect(-worldViewDir, worldNormal);
                half4 skyData = UNITY_SAMPLE_TEXCUBE(unity_SpecCube0, worldRefl);
                half3 skyColor = DecodeHDR (skyData, unity_SpecCube0_HDR);                
                fixed4 c = 0;
                c.rgb = skyColor;

                // modulate sky color with the base texture, and the occlusion map
                fixed3 baseColor = tex2D(_MainTex, i.uv).rgb;
                fixed occlusion = tex2D(_OcclusionMap, i.uv).r;
                c.rgb *= baseColor;
                c.rgb *= occlusion;

                return c;
            }
            ENDCG
        }
    }
}
</code></pre><p>Balloon cat is looking good!</p>
<p><img src="../uploads/SL/ExampleMoreTextures.png" alt=""></p>
<h2 id="texturing-shader-examples">Texturing shader examples</h2>
<h4 id="procedural-checkerboard-pattern">Procedural checkerboard pattern</h4>
<p>Here&#39;s a shader that outputs a checkerboard pattern based on texture coordinates of a mesh:</p>
<pre><code>Shader &quot;Unlit/Checkerboard&quot;
{
    Properties
    {
        _Density (&quot;Density&quot;, Range(2,50)) = 30
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;

            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 vertex : SV_POSITION;
            };

            float _Density;

            v2f vert (float4 pos : POSITION, float2 uv : TEXCOORD0)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(pos);
                o.uv = uv * _Density;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float2 c = i.uv;
                c = floor(c) / 2;
                float checker = frac(c.x + c.y) * 2;
                return checker;
            }
            ENDCG
        }
    }
}
</code></pre><p>The density slider in the <a href="SL-Properties.html">Properties</a> block controls how dense the checkerboard is. In the vertex shader, the mesh UVs are multiplied by the density value to take them from a range of 0 to 1 to a range of 0 to density. Let&#39;s say the density was set to 30 - this will make <strong>i.uv</strong> input into the fragment shader contain floating point values from zero to 30 for various places of the mesh being rendered.</p>
<p>Then the fragment shader code takes only the integer part of the input coordinate using HLSL&#39;s built-in <strong>floor</strong> function, and divides it by two. Recall that the input coordinates were numbers from 0 to 30; this makes them all be &quot;quantized&quot; to values of 0, 0.5, 1, 1.5, 2, 2.5, and so on. This was done on both the x and y components of the input coordinate.</p>
<p>Next up, we add these x and y coordinates together (each of them only having possible values of 0, 0.5, 1, 1.5, ...) and only take the fractional part using another built-in HLSL function, <strong>frac</strong>. Result of this can only be either 0.0 or 0.5. We then multiply it by two to make it either 0.0 or 1.0, and output as a color (this results in black or white color respectively).</p>
<p><img src="../uploads/SL/ExampleCheckerboard.png" alt=""></p>
<h4 id="tri-planar-texturing">Tri-planar texturing</h4>
<p>For complex or procedural meshes, instead of texturing them using the regular UV coordinates, it is sometimes useful to just &quot;project&quot; texture onto the object from three primary directions. This is called &quot;tri-planar&quot; texturing. The idea is to use surface normal to weight the three texture directions. Here&#39;s the shader:</p>
<pre><code>Shader &quot;Unlit/Triplanar&quot;
{
    Properties
    {
        _MainTex (&quot;Texture&quot;, 2D) = &quot;white&quot; {}
        _Tiling (&quot;Tiling&quot;, Float) = 1.0
        _OcclusionMap(&quot;Occlusion&quot;, 2D) = &quot;white&quot; {}
    }
    SubShader
    {
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;

            struct v2f
            {
                half3 objNormal : TEXCOORD0;
                float3 coords : TEXCOORD1;
                float2 uv : TEXCOORD2;
                float4 pos : SV_POSITION;
            };

            float _Tiling;

            v2f vert (float4 pos : POSITION, float3 normal : NORMAL, float2 uv : TEXCOORD0)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(pos);
                o.coords = pos.xyz * _Tiling;
                o.objNormal = normal;
                o.uv = uv;
                return o;
            }

            sampler2D _MainTex;
            sampler2D _OcclusionMap;

            fixed4 frag (v2f i) : SV_Target
            {
                // use absolute value of normal as texture weights
                half3 blend = abs(i.objNormal);
                // make sure the weights sum up to 1 (divide by sum of x+y+z)
                blend /= dot(blend,1.0);
                // read the three texture projections, for x,y,z axes
                fixed4 cx = tex2D(_MainTex, i.coords.yz);
                fixed4 cy = tex2D(_MainTex, i.coords.xz);
                fixed4 cz = tex2D(_MainTex, i.coords.xy);
                // blend the textures based on weights
                fixed4 c = cx * blend.x + cy * blend.y + cz * blend.z;
                // modulate by regular occlusion map
                c *= tex2D(_OcclusionMap, i.uv);
                return c;
            }
            ENDCG
        }
    }
}
</code></pre><p><img src="../uploads/SL/ExampleTriPlanar.png" alt=""></p>
<h2 id="calculating-lighting">Calculating lighting</h2>
<p>Typically when you want a shader that works with Unity&#39;s lighting pipeline, you
would write a <a href="SL-SurfaceShaders.html">surface shader</a>. This does most of the &quot;heavy lifting&quot;
for you, and your shader code just needs to define surface properties.</p>
<p>However in some cases you want to bypass the standard surface shader path; either because
you want to only support some limited subset of whole lighting pipeline for performance reasons,
or you want to do custom things that aren&#39;t quite &quot;standard lighting&quot;. The following examples
will show how to get to the lighting data from manually-written vertex and fragment shaders.
Looking at the code generated by surface shaders (via <a href="class-Shader.html">shader inspector</a>) is also
a good learning resource.</p>
<h4 id="simple-diffuse-lighting">Simple diffuse lighting</h4>
<p>The first thing we need to do is to indicate that our shader does in fact need lighting information passed to it. Unity&#39;s <a href="SL-RenderPipeline.html">rendering pipeline</a> supports various ways of rendering; here we&#39;ll be using the default <a href="RenderTech-ForwardRendering.html">forward rendering</a> one.</p>
<p>We&#39;ll start by only supporting one directional light. Forward rendering in Unity works by rendering the main directional light, ambient, lightmaps and reflections in a single pass called <strong>ForwardBase</strong>. In the shader, this is indicated by adding a <a href="SL-PassTags.html">pass tag</a>: <strong>Tags {&quot;LightMode&quot;=&quot;ForwardBase&quot;}</strong>. This will make directional light data be passed into shader via some <a href="SL-UnityShaderVariables.html">built-in variables</a>.</p>
<p>Here&#39;s the shader that computes simple diffuse lighting per vertex, and uses a single main texture:</p>
<pre><code>Shader &quot;Lit/Simple Diffuse&quot;
{
    Properties
    {
        [NoScaleOffset] _MainTex (&quot;Texture&quot;, 2D) = &quot;white&quot; {}
    }
    SubShader
    {
        Pass
        {
            // indicate that our pass is the &quot;base&quot; pass in forward
            // rendering pipeline. It gets ambient and main directional
            // light data set up; light direction in _WorldSpaceLightPos0
            // and color in _LightColor0
            Tags {&quot;LightMode&quot;=&quot;ForwardBase&quot;}

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot; // for UnityObjectToWorldNormal
            #include &quot;UnityLightingCommon.cginc&quot; // for _LightColor0

            struct v2f
            {
                float2 uv : TEXCOORD0;
                fixed4 diff : COLOR0; // diffuse lighting color
                float4 vertex : SV_POSITION;
            };

            v2f vert (appdata_base v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.texcoord;
                // get vertex normal in world space
                half3 worldNormal = UnityObjectToWorldNormal(v.normal);
                // dot product between normal and light direction for
                // standard diffuse (Lambert) lighting
                half nl = max(0, dot(worldNormal, _WorldSpaceLightPos0.xyz));
                // factor in the light color
                o.diff = nl * _LightColor0;
                return o;
            }

            sampler2D _MainTex;

            fixed4 frag (v2f i) : SV_Target
            {
                // sample texture
                fixed4 col = tex2D(_MainTex, i.uv);
                // multiply by lighting
                col *= i.diff;
                return col;
            }
            ENDCG
        }
    }
}
</code></pre><p>This makes the object react to light direction - parts of it facing the light are illuminated, and parts facing away are not illuminated at all.</p>
<p><img src="../uploads/SL/ExampleDiffuseLighting.png" alt=""></p>
<h4 id="diffuse-lighting-with-ambient">Diffuse lighting with ambient</h4>
<p>The example above does not take any ambient lighting or light probes into account. Let&#39;s fix this!
It turns out we can do this by adding just a single line of code. Both ambient and <a href="LightProbes.html">light probe</a> data is passed to shaders in Spherical Harmonics form, and <strong>ShadeSH9</strong> function from <strong>UnityCG.cginc</strong> <a href="SL-BuiltinIncludes.html">include file</a> does all the work of evaluating it, given a world space normal.</p>
<pre><code>Shader &quot;Lit/Diffuse With Ambient&quot;
{
    Properties
    {
        [NoScaleOffset] _MainTex (&quot;Texture&quot;, 2D) = &quot;white&quot; {}
    }
    SubShader
    {
        Pass
        {
            Tags {&quot;LightMode&quot;=&quot;ForwardBase&quot;}

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;
            #include &quot;UnityLightingCommon.cginc&quot;

            struct v2f
            {
                float2 uv : TEXCOORD0;
                fixed4 diff : COLOR0;
                float4 vertex : SV_POSITION;
            };

            v2f vert (appdata_base v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.texcoord;
                half3 worldNormal = UnityObjectToWorldNormal(v.normal);
                half nl = max(0, dot(worldNormal, _WorldSpaceLightPos0.xyz));
                o.diff = nl * _LightColor0;

                // the only difference from previous shader:
                // in addition to the diffuse lighting from the main light,
                // add illumination from ambient or light probes
                // ShadeSH9 function from UnityCG.cginc evaluates it,
                // using world space normal
                o.diff.rgb += ShadeSH9(half4(worldNormal,1));
                return o;
            }

            sampler2D _MainTex;

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv);
                col *= i.diff;
                return col;
            }
            ENDCG
        }
    }
}
</code></pre><p>This shader is in fact starting to look very similar to the built-in <a href="shader-NormalDiffuse.html">Legacy Diffuse</a> shader!</p>
<p><img src="../uploads/SL/ExampleDiffuseAmbientLighting.png" alt=""></p>
<h4 id="implementing-shadow-casting">Implementing shadow casting</h4>
<p>Our shader currently can neither receive nor cast shadows. Let&#39;s implement shadow casting first.</p>
<p>In order to cast shadows, a shader has to have a <strong>ShadowCaster</strong> <a href="SL-PassTags.html">pass type</a> in any of its <a href="SL-SubShader.html">subshaders</a> or any <a href="SL-Fallback.html">fallback</a>. The ShadowCaster pass is used to render the object into the shadowmap, and typically it is fairly simple - the vertex shader only needs to evaluate the vertex position, and the fragment shader pretty much does not do anything. The shadowmap is only the depth buffer, so even the color output by the fragment shader does not really matter.</p>
<p>This means that for a lot of shaders, the shadow caster pass is going to be almost exactly the same (unless object has custom vertex shader based deformations, or has alpha cutout / semitransparent parts). The easiest way to pull it in is via <a href="SL-UsePass.html">UsePass</a> shader command:</p>
<pre><code>Pass
{
    // regular lighting pass
}
// pull in shadow caster from VertexLit built-in shader
UsePass &quot;Legacy Shaders/VertexLit/SHADOWCASTER&quot;
</code></pre><p>However we&#39;re learning here, so let&#39;s do the same thing &quot;by hand&quot; so to speak. For shorter code,
we&#39;ve replaced the lighting pass (&quot;ForwardBase&quot;) with code that only does untextured ambient. Below it, there&#39;s a &quot;ShadowCaster&quot; pass that makes the object support shadow casting.</p>
<pre><code>Shader &quot;Lit/Shadow Casting&quot;
{
    SubShader
    {
        // very simple lighting pass, that only does non-textured ambient
        Pass
        {
            Tags {&quot;LightMode&quot;=&quot;ForwardBase&quot;}
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;
            struct v2f
            {
                fixed4 diff : COLOR0;
                float4 vertex : SV_POSITION;
            };
            v2f vert (appdata_base v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                half3 worldNormal = UnityObjectToWorldNormal(v.normal);
                // only evaluate ambient
                o.diff.rgb = ShadeSH9(half4(worldNormal,1));
                o.diff.a = 1;
                return o;
            }
            fixed4 frag (v2f i) : SV_Target
            {
                return i.diff;
            }
            ENDCG
        }

        // shadow caster rendering pass, implemented manually
        // using macros from UnityCG.cginc
        Pass
        {
            Tags {&quot;LightMode&quot;=&quot;ShadowCaster&quot;}

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma multi_compile_shadowcaster
            #include &quot;UnityCG.cginc&quot;

            struct v2f { 
                V2F_SHADOW_CASTER;
            };

            v2f vert(appdata_base v)
            {
                v2f o;
                TRANSFER_SHADOW_CASTER_NORMALOFFSET(o)
                return o;
            }

            float4 frag(v2f i) : SV_Target
            {
                SHADOW_CASTER_FRAGMENT(i)
            }
            ENDCG
        }
    }
}
</code></pre><p>Now there&#39;s a plane underneath, using a regular built-in Diffuse shader, so that we can see
our shadows working (remember, our current shader does not support <em>receiving</em> shadows yet!).</p>
<p><img src="../uploads/SL/ExampleShadowCasting.png" alt=""></p>
<p>We&#39;ve used the <strong>#pragma multi_compile_shadowcaster</strong> directive. This causes the shader to be compiled into several variants with different preprocessor macros defined for each (see
<a href="SL-MultipleProgramVariants.html">multiple shader variants</a> page for details). When rendering into the shadowmap, the cases of point lights vs other light types need slightly different shader code, that&#39;s why this directive is needed.</p>
<h4 id="receiving-shadows">Receiving shadows</h4>
<p>Implementing support for receiving shadows will require compiling the base lighting pass into
several variants, to handle cases of &quot;directional light without shadows&quot; and &quot;directional light with shadows&quot; properly. <strong>#pragma multi_compile_fwdbase</strong> directive does this (see
<a href="SL-MultipleProgramVariants.html">multiple shader variants</a> for details). In fact it does a lot more:
it also compiles variants for the different lightmap types, realtime GI being on or off etc. Currently we don&#39;t need all that, so we&#39;ll explicitly skip these variants.</p>
<p>Then to get actual shadowing computations, we&#39;ll <strong>#include &quot;AutoLight.cginc&quot;</strong> shader <a href="SL-BuiltinIncludes.html">include file</a> and use SHADOW_COORDS, TRANSFER_SHADOW, SHADOW_ATTENUATION macros from it.</p>
<p>Here&#39;s the shader:</p>
<pre><code>Shader &quot;Lit/Diffuse With Shadows&quot;
{
    Properties
    {
        [NoScaleOffset] _MainTex (&quot;Texture&quot;, 2D) = &quot;white&quot; {}
    }
    SubShader
    {
        Pass
        {
            Tags {&quot;LightMode&quot;=&quot;ForwardBase&quot;}
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include &quot;UnityCG.cginc&quot;
            #include &quot;Lighting.cginc&quot;

            // compile shader into multiple variants, with and without shadows
            // (we don&#39;t care about any lightmaps yet, so skip these variants)
            #pragma multi_compile_fwdbase nolightmap nodirlightmap nodynlightmap novertexlight
            // shadow helper functions and macros
            #include &quot;AutoLight.cginc&quot;

            struct v2f
            {
                float2 uv : TEXCOORD0;
                SHADOW_COORDS(1) // put shadows data into TEXCOORD1
                fixed3 diff : COLOR0;
                fixed3 ambient : COLOR1;
                float4 pos : SV_POSITION;
            };
            v2f vert (appdata_base v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = v.texcoord;
                half3 worldNormal = UnityObjectToWorldNormal(v.normal);
                half nl = max(0, dot(worldNormal, _WorldSpaceLightPos0.xyz));
                o.diff = nl * _LightColor0.rgb;
                o.ambient = ShadeSH9(half4(worldNormal,1));
                // compute shadows data
                TRANSFER_SHADOW(o)
                return o;
            }

            sampler2D _MainTex;

            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 col = tex2D(_MainTex, i.uv);
                // compute shadow attenuation (1.0 = fully lit, 0.0 = fully shadowed)
                fixed shadow = SHADOW_ATTENUATION(i);
                // darken light&#39;s illumination with shadow, keep ambient intact
                fixed3 lighting = i.diff * shadow + i.ambient;
                col.rgb *= lighting;
                return col;
            }
            ENDCG
        }

        // shadow casting support
        UsePass &quot;Legacy Shaders/VertexLit/SHADOWCASTER&quot;
    }
}
</code></pre><p>Look, we have shadows now!</p>
<p><img src="../uploads/SL/ExampleShadowReceiving.png" alt=""></p>
<h2 id="other-shader-examples">Other shader examples</h2>
<h3 id="fog">Fog</h3>
<pre><code>Shader &quot;Custom/TextureCoordinates/Fog&quot; {
    SubShader {
        Pass {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            //Needed for fog variation to be compiled.
            #pragma multi_compile_fog

            #include &quot;UnityCG.cginc&quot;

            struct vertexInput {
                float4 vertex : POSITION;
                float4 texcoord0 : TEXCOORD0;
            };

            struct fragmentInput{
                float4 position : SV_POSITION;
                float4 texcoord0 : TEXCOORD0;

                //Used to pass fog amount around number should be a free texcoord.
                UNITY_FOG_COORDS(1)
            };

            fragmentInput vert(vertexInput i){
                fragmentInput o;
                o.position = UnityObjectToClipPos(i.vertex);
                o.texcoord0 = i.texcoord0;

                //Compute fog amount from clip space position.
                UNITY_TRANSFER_FOG(o,o.position);
                return o;
            }

            fixed4 frag(fragmentInput i) : SV_Target {
                fixed4 color = fixed4(i.texcoord0.xy,0,0);

                //Apply fog (additive pass are automatically handled)
                UNITY_APPLY_FOG(i.fogCoord, color); 

                //to handle custom fog color another option would have been 
                //#ifdef UNITY_PASS_FORWARDADD
                //  UNITY_APPLY_FOG_COLOR(i.fogCoord, color, float4(0,0,0,0));
                //#else
                //  fixed4 myCustomColor = fixed4(0,0,1,0);
                //  UNITY_APPLY_FOG_COLOR(i.fogCoord, color, myCustomColor);
                //#endif

                return color;
            }
            ENDCG
        }
    }
}
</code></pre><p>You can download the examples shown above as a <a href="../uploads/Examples/UnityShaderDocExamples.zip">zipped Unity project</a>.</p>
<h2 id="further-reading">Further reading</h2>
<ul>
<li><a href="SL-ShaderPrograms.html">Writing Vertex and Fragment Programs</a>.</li>
<li><a href="SL-ShaderSemantics.html">Shader Semantics</a>.</li>
<li><a href="SL-SurfaceShaders.html">Writing Surface Shaders</a>.</li>
<li><a href="SL-Reference.html">Shader Reference</a>.</li>
</ul>
</article>
          </div>
          
          <div class="hidden-sm col-md-2" role="complementary">
            <div class="sideaffix">
              <div class="contribution">
                <ul class="nav">
                </ul>
              </div>
              <nav class="bs-docs-sidebar hidden-print hidden-xs hidden-sm affix" id="affix">
              <!-- <p><a class="back-to-top" href="#top">Back to top</a><p> -->
              </nav>
            </div>
          </div>
        </div>
      </div>
      
      <footer>
        <div class="grad-bottom"></div>
        <div class="footer">
          <div class="container">
            <span class="pull-right">
              <a href="#top">Back to top</a>
            </span>
            
            <span>Copyright © 2019 Unity Technologies<br>Generated by <strong>DocFX</strong></span>
          </div>
        </div>
      </footer>
    </div>
    
    <script type="text/javascript" src="../../styles/docfx.vendor.js"></script>
    <script type="text/javascript" src="../../styles/docfx.js"></script>
    <script type="text/javascript" src="../../styles/main.js"></script>
  </body>
</html>
